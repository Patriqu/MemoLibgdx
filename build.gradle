buildscript {
	repositories {
		mavenCentral()
		mavenLocal()
		google()
		gradlePluginPortal()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
	}
	dependencies {
		classpath "com.android.tools.build:gradle:$androidPluginVersion"
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
		classpath "org.wisepersist:gwt-gradle-plugin:$gwtPluginVersion"

		constraints {
			classpath("org.apache.logging.log4j:log4j-core") {
				version {
					strictly("[2.16, 3[")
					prefer("2.16.0")
				}
				because("CVE-2021-44228: Log4j vulnerable to remote code execution")
			}
		}
	}
}

allprojects {
	apply plugin: 'eclipse'
	apply plugin: 'idea'
}

configure(subprojects - project(':android')) {
	apply plugin: 'java-library'
	apply plugin: 'kotlin'
	sourceCompatibility = 15
	compileJava {
		options.incremental = true
	}
}

subprojects {
	version = '0.2.3.1'
	ext.appName = 'MemoLibgdx'
	repositories {
		mavenCentral()
		mavenLocal()
		gradlePluginPortal()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
		maven { url 'https://jitpack.io' }
		maven { url 'https://s01.oss.sonatype.org' }
	}
}

eclipse.project.name = 'MemoLibgdx' + '-parent'
