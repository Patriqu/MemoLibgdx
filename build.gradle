import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
	repositories {
		mavenCentral()
		mavenLocal()
		google()
		gradlePluginPortal()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
		maven { url 'https://jitpack.io' }
	}
	dependencies {
		classpath "com.android.tools.build:gradle:$androidGradlePluginVersion"
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
		classpath "org.wisepersist:gwt-gradle-plugin:$gwtPluginVersion"
		classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:$sonarqubePluginVersion"

		constraints {
			classpath("org.apache.logging.log4j:log4j-core") {
				version {
					strictly("[2.18, 3[")
					prefer("2.18.0")
				}
				because("CVE-2021-44228: Log4j vulnerable to remote code execution")
			}
		}
	}
}

allprojects {
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	apply plugin: "org.sonarqube"
}

configure(subprojects - project(':android')) {
	apply plugin: 'java-library'
	apply plugin: 'kotlin'
	sourceCompatibility = JavaVersion.VERSION_21

	compileJava {
		options.incremental = true
	}

	tasks.withType(KotlinCompile).configureEach {
		kotlinOptions {
			//languageVersion = "$kotlinLanguageVersion"
			jvmTarget = "$jvmTargetVersion"
			freeCompilerArgs += "-Xallow-unstable-dependencies"
			incremental = true
		}
	}
}

subprojects {
	version = '$gameVersion'
	ext.appName = 'MemoLibgdx'
	repositories {
		mavenCentral()
		mavenLocal()
		gradlePluginPortal()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
		maven { url 'https://jitpack.io' }
		maven { url 'https://s01.oss.sonatype.org' }
	}
}

eclipse.project.name = 'MemoLibgdx' + '-parent'
